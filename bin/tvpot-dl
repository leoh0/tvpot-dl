#!/usr/bin/env perl
#
# tvpot-dl - Download flash videos from Daum tvpot
#
# tvpot-dl downloads flash videos from Daum tvpot.
#
# It can also be used to download movie trailers or music videos from
# Daum.
#
# Seungwon Jeong <seungwon0@gmail.com>
#
# Copyright (C) 2011 by Seungwon Jeong

use strict;

use warnings;

use 5.010;

use autodie;

use Getopt::Long;

use LWP::Simple qw< getstore is_error $ua >;

use App::TvpotDl;

sub print_version {
    say "tvpot-dl $App::TvpotDl::VERSION";
    return;
}

sub print_help {
    print_version();

    print <<"END_HELP";

Usage:

  tvpot-dl [options] URL...

Options:

  -h, --help            print this help text and exit
  -v, --version         print program version and exit
  -q, --quiet           activates quiet mode
  -u, --url             quiet but print URL
  -g, --get-title       quiet but print title
  -t, --title           use title in file name

Argument:

  URL                   URL of a tvpot video

Examples:

  tvpot-dl 'http://tvpot.daum.net/clip/ClipView.do?clipid=29772622'
  tvpot-dl 'http://music.daum.net/song/songVideo.do?songId=8443911&videoId=8446'
  tvpot-dl 'http://movie.daum.net/moviedetail/moviedetailVideoView.do?movieId=52843&videoId=29850'
  tvpot-dl --url 'http://tvpot.daum.net/brand/ProgramClipView.do?ownerid=Sa3QtEuMXf10&playlistid=1382723&clipid=25537851' | xargs wget --continue
  tvpot-dl < url_list

Please report bugs to <seungwon0\@gmail.com>.
END_HELP
    return;
}

sub download_video {
    my ( $url, $arg_ref ) = @_;
    my $quiet_mode = $arg_ref->{quiet_mode};
    my $get_url    = $arg_ref->{get_url};
    my $get_title  = $arg_ref->{get_title};
    my $title      = $arg_ref->{title};

    # Get video ID
    my $video_id = App::TvpotDl::get_video_id($url);
    return if !defined $video_id;
    if ( !$quiet_mode ) {
        say "Video ID: $video_id";
    }

    # Get video URL
    my $video_url = App::TvpotDl::get_video_url($video_id);
    return if !defined $video_url;
    if ($get_url) {
        say $video_url;
        return 1;
    }
    if ( !$quiet_mode ) {
        say "Video URL: $video_url";
    }

    # Get video title
    my $video_title = App::TvpotDl::get_video_title($video_id);
    return if !defined $video_title;
    if ($get_title) {
        say $video_title;
        return 1;
    }
    if ( !$quiet_mode ) {
        say "Video title: $video_title";
    }

    # Download the video
    my $file_name = $title ? "$video_title.flv" : "$video_id.flv";
    if ( !$quiet_mode ) {
        say "Downloading the video as '$file_name'... "
            . '(It may takes several minutes.)';
    }
    if ( !$quiet_mode ) {
        $ua->show_progress(1);
    }
    my $rc = getstore( $video_url, $file_name );
    $ua->show_progress(0);
    if ( is_error($rc) ) {
        warn "Cannot download the video.\n";
        return;
    }

    if ( !$quiet_mode ) {
        say "Successfully downloaded '$file_name'.";
    }

    return 1;
}

my $print_help;
my $print_version;

my $quiet_mode;
my $get_url;
my $get_title;
my $title;

GetOptions(
    'help'      => \$print_help,
    'version'   => \$print_version,
    'quiet'     => \$quiet_mode,
    'url'       => \$get_url,
    'get-title' => \$get_title,
    'title'     => \$title,
) or die "GetOptions() failed!\n";

$quiet_mode = $quiet_mode || $get_url || $get_title;

if ($print_help) {
    print_help();
    exit;
}

if ($print_version) {
    print_version();
    exit;
}

my @urls = @ARGV;

# tvpot-dl < url_list
if ( !-t STDIN ) {
    while ( defined( my $line = <STDIN> ) ) {
        chomp $line;

        $line =~ s/[ #] .*//xms;    # Remove comment
        $line =~ s/\s+//xmsg;       # Trim spaces

        if ( $line ne q{} ) {
            push @urls, $line;
        }
    }
}

if ( @urls < 1 ) {
    print_help();
    exit 2;
}

my $arg_ref = {
    quiet_mode => $quiet_mode,
    get_url    => $get_url,
    get_title  => $get_title,
    title      => $title,
};

for my $i ( 0 .. $#urls ) {
    my $url = $urls[$i];

    if ( !$quiet_mode && @urls > 1 ) {
        printf "[%d/%d] %s\n", $i + 1, scalar @urls, $url;
    }

    download_video( $url, $arg_ref );

    if ( !$quiet_mode && @urls > 1 ) {
        say q{};    # Blank line
    }
}

__END__

=head1 NAME

tvpot-dl - download flash videos from Daum tvpot

=head1 SYNOPSIS

tvpot-dl [options] URL ...

=head1 DESCRIPTION

tvpot-dl downloads flash videos from Daum tvpot.

It can also be used to download movie trailers or music videos from
Daum.

=head1 ARGUMENTS

=over

=item URL

URL of a tvpot video.

=back

=head1 OPTIONS

=over

=item -h, --help

Print help text and exit.

=item -v, --version

Print program version and exit.

=item -q, --quiet

Activates quiet mode, avoiding many messages being written to the
terminal.

=item -u, --url

Simulate the operation, like quiet mode, but show the URL that would
be used to download the video. Can be used with other download tools
like wget.

=item -g, --get-title

Simulate the operation, like quiet mode, but show the title of the
video that would be downloaded.

=item -t, --title

Use the title of the video in the file name used to download the
video.

=back

=head1 EXAMPLES

Download a tvpot video:

	tvpot-dl 'http://tvpot.daum.net/clip/ClipView.do?clipid=29772622'

Download a music video:

	tvpot-dl 'http://music.daum.net/song/songVideo.do?songId=8443911&videoId=8446'

Download a movie trailer:

	tvpot-dl 'http://movie.daum.net/moviedetail/moviedetailVideoView.do?movieId=52843&videoId=29850'

Download a video using wget:

	tvpot-dl --url 'http://tvpot.daum.net/brand/ProgramClipView.do?ownerid=Sa3QtEuMXf10&playlistid=1382723&clipid=25537851' | xargs wget --continue

Download videos from url_list file:

	tvpot-dl < url_list

=head1 URL

L<https://github.com/seungwon0/tvpot-dl>

=head1 AUTHOR

Seungwon Jeong E<lt>seungwon0@gmail.comE<gt>

=head1 LICENSE AND COPYRIGHT

Copyright 2011 Seungwon Jeong.

This program is free software; you can redistribute it and/or modify it
under the terms of either: the GNU General Public License as published
by the Free Software Foundation; or the Artistic License.

See http://dev.perl.org/licenses/ for more information.

=head1 SEE ALSO

youtube-dl(1), get_flash_videos(1)

=cut
